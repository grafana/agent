Unicode true
# This installs two files, agent-windows-amd64.exe and logo.ico, creates a start menu shortcut, builds an uninstaller, and
# adds uninstall information to the registry for Add/Remove Programs

!define APPNAME "Grafana Agent"
!define DESCRIPTION "The Grafana Agent collects observability data and sends it to a compatible remote write endpoint"
# These will be displayed by the "Click here for support information" link in "Add/Remove Programs"
!define HELPURL "https://github.com/grafana/agent/discussions" # "Support Information" link
!define UPDATEURL "https://github.com/grafana/agent/releases" # "Product Updates" link
!define ABOUTURL "https://github.com/grafana/agent" # "Publisher" link
!include LogicLib.nsh

RequestExecutionLevel admin #Require admin rights on NT6+ (When UAC is turned on)

InstallDir "$PROGRAMFILES\${APPNAME}"

# This will be in the installer/uninstaller's title bar
Name "${APPNAME} ${VERSION}"
Icon "logo.ico"
outFile "/app/grafana-agent-installer.exe"

!include LogicLib.nsh
!include nsDialogs.nsh
!include FileFunc.nsh


# Everything must be global Vars

# These are handles to dialogs
Var Dialog
Var RemoteWriteLabel
Var RemoteWriteText

Var RemoteWriteUserLabel
Var RemoteWriteUserText

Var RemoteWritePasswordLabel
Var RemoteWritePasswordText

Var EnableExporterLabel
Var EnableExporterCheck

# These are strings that hold the values

Var RemoteUrl
Var RemoteUser
Var RemotePassword
Var EnableExporterValue

# Silent install params

Var PassedInParameters

Page directory
Page custom remoteWriteInfo remoteWriteInfoLeave
Page custom enableWindowsExporter enableWindowsExporterLeave
Page instfiles



!macro VerifyUserIsAdmin
UserInfo::GetAccountType
pop $0
${If} $0 != "admin" #Require admin rights on NT4+
        messageBox mb_iconstop "Administrator rights required!"
        setErrorLevel 740 #ERROR_ELEVATION_REQUIRED
        quit
${EndIf}
!macroend


Section SilentInstaller
    IfSilent ThisIsSilent RunInstaller
    ThisIsSilent:
        ${GetParameters} $PassedInParameters
        ${GetOptions} $PassedInParameters "/RemoteURL" $RemoteUrl
        ${GetOptions} $PassedInParameters "/RemoteUser" $RemoteUser
        ${GetOptions} $PassedInParameters "/RemotePassword" $RemotePassword
        ${GetOptions} $PassedInParameters "/EnableExporter" $EnableExporterValue
        Call Install
        # TODO add error conditions
        Return
    RunInstaller:
        Return
SectionEnd


Section "install"
    IfSilent ThisIsSilent RunInstaller
    ThisIsSilent:
        Return
    RunInstaller:
        Call Install
SectionEnd

Function remoteWriteInfo
    nsDialogs::Create 1018
    Pop $Dialog

    ${If} $Dialog == error
        Abort
    ${EndIf}
    ${NSD_CreateLabel} 0 0 100% 12u "Enter URL for remote write"
    Pop $RemoteWriteLabel

    ${NSD_CreateText} 0 13u 100% 12u ""
    Pop $RemoteWriteText

    ${NSD_CreateLabel} 0 26u 100% 12u "Enter User for remote write"
    Pop $RemoteWriteUserLabel

    ${NSD_CreateText} 0 39u 100% 12u ""
    Pop $RemoteWriteUserText

    ${NSD_CreateLabel} 0 52u 100% 12u "Enter Password for remote write"
    Pop $RemoteWritePasswordLabel

    ${NSD_CreateText} 0 65u 100% 12u ""
    Pop $RemoteWritePasswordText

    nsDialogs::Show
FunctionEnd


Function remoteWriteInfoLeave
    ${NSD_GetText}  $RemoteWriteText  $RemoteUrl
    ${NSD_GetText}  $RemoteWriteUserText  $RemoteUser
    ${NSD_GetText}  $RemoteWritePasswordText  $RemotePassword
FunctionEnd

Function enableWindowsExporter
    nsDialogs::Create 1018
    Pop $Dialog

    ${If} $Dialog == error
        Abort
    ${EndIf}

    ${NSD_CreateLabel} 0 0 100% 12u "Enable Windows Exporter"
    Pop $EnableExporterLabel

    ${NSD_CreateCheckBox} 0 13u 100% 12u ""
    Pop $EnableExporterCheck

    nsDialogs::Show
FunctionEnd

Function enableWindowsExporterLeave
    ${NSD_GetState} $EnableExporterCheck $EnableExporterValue
    ${If} $EnableExporterValue == ${BST_CHECKED}
        StrCpy $EnableExporterValue "true"
    ${Else}
        StrCpy $EnableExporterValue "false"
    ${EndIf}
FunctionEnd


Function .onInit
    setShellVarContext all
    !insertmacro VerifyUserIsAdmin
FunctionEnd

# Uninstaller
Function un.onInit
    SetShellVarContext all

    #Verify the uninstaller - last chance to back out
    MessageBox MB_OKCANCEL "Permanantly remove ${APPNAME}?" IDOK next
        Abort
    next:
    !insertmacro VerifyUserIsAdmin
FunctionEnd

Function Install
    # Files for the install directory - to build the installer, these should be in the same directory as the install script (this file)
    setOutPath $INSTDIR
    # Files added here should be removed by the uninstaller (see section "uninstall")
    file "agent-windows-amd64.exe"
    file "logo.ico"
    # Add any other files for the install directory (license files, app data, etc) here

    # Uninstaller - See function un.onInit and section "uninstall" for configuration
    writeUninstaller "$INSTDIR\uninstall.exe"

    # Registry information for add/remove programs
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "DisplayName" "${APPNAME} ${VERSION} - ${DESCRIPTION}"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "InstallLocation" "$\"$INSTDIR$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "DisplayIcon" "$\"$INSTDIR\logo.ico$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "Publisher" "$\"https://github.com/grafana/agent$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "HelpLink" "$\"${HELPURL}$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "URLUpdateInfo" "$\"${UPDATEURL}$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "URLInfoAbout" "$\"${ABOUTURL}$\""
    # There is no option for modifying or repairing the install
    WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "NoModify" 1
    WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" "NoRepair" 1
    Call WriteConfig

    # Create our batch file, since services cant run with parameters, nsexec is used to surpress console output, instead goes
    # to NSIS log window
    nsExec::ExecToLog 'sc create "Grafana Agent" binpath= "$INSTDIR\agent-windows-amd64.exe"'
    Pop $0
    # These separate create and config commands are needed, on the config the binpath is required
    nsExec::ExecToLog 'sc config "Grafana Agent" binpath= "$INSTDIR\agent-windows-amd64.exe -config.file=\"$INSTDIR\agent-config.yaml\""'
    Pop $0
    nsExec::ExecToLog `sc start "Grafana Agent"`
    Pop $0
FunctionEnd


Section "uninstall"
    # Remove Start Menu launcher
    delete "$SMPROGRAMS\${APPNAME}\${APPNAME}.lnk"
    # Try to remove the Start Menu folder - this will only happen if it is empty
    rmDir "$SMPROGRAMS\${APPNAME}"
    # This is cleanup on the service and removing the exporter.
    nsExec::ExecToLog `sc stop "Grafana Agent"`
    Pop $0
    nsExec::ExecToLog `sc delete "Grafana Agent" confirm`
    Pop $0

    # Remove files
    delete $INSTDIR\agent-windows-amd64.exe
    delete $INSTDIR\logo.ico
    delete $INSTDIR\agent-config.yaml

    # Always delete uninstaller as the last action
    delete $INSTDIR\uninstall.exe

    # Try to remove the install directory - this will only happen if it is empty
    rmDir $INSTDIR

    # Remove uninstaller information from the registry
    DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}"
SectionEnd


Function WriteConfig
     # Write the config file, its easier to do this way than replacing an values in a templated file
    FileOpen $9 "$INSTDIR\agent-config.yaml" w #Opens a Empty File and fills it
    FileWrite $9 `server:$\n`
    FileWrite $9 `  http_listen_port: 12345$\n`
    FileWrite $9 `prometheus:$\n`
    FileWrite $9 `  wal_directory: $INSTDIR\grafana-agent-wal$\n`
    FileWrite $9 `  global:$\n`
    FileWrite $9 `    scrape_interval: 15s$\n`
    FileWrite $9 `  configs:$\n`
    FileWrite $9 `    - name: integrations$\n`
    FileWrite $9 `      scrape_configs:$\n`
    FileWrite $9 `  remote_write:$\n`
    FileWrite $9 `    - url: $RemoteUrl$\n`
    FileWrite $9 `      basic_auth:$\n`
    FileWrite $9 `        username: "$RemoteUser"$\n`
    FileWrite $9 `        password: $RemotePassword$\n`
    ${If} $EnableExporterValue == "true"
    FileWrite $9 `integrations:$\n`
    FileWrite $9 `  windows_exporter:$\n`
    FileWrite $9 `    enabled: true`
    ${EndIf}
    FileClose $9 # and close the file
    Return
FunctionEnd
